plugins {
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'application'
}

defaultTasks 'clean', 'licenseFormat', 'build'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

archivesBaseName = project.name.toLowerCase().replace(' ', '_')
version = "${project.version}"
mainClassName = "${project.mainClass}"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'oss-sonatype-public'
        url = 'https://oss.sonatype.org/content/repositories/public/'
    }
}

dependencies {
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.google.inject:guice:4.1.0'
    compile 'ninja.leaping.configurate:configurate-hocon:3.3'
    compile 'ninja.leaping.configurate:configurate-gson:3.3'
    compile 'ninja.leaping.configurate:configurate-yaml:3.3'
    compile 'org.slf4j:slf4j-api:1.7.25'
    // TODO This needs to only be in the impl of Spongie
    compile 'org.ow2.asm:asm-debug-all:5.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.8.2'
    // TODO This needs to only be in IRC module
    compile 'org.kitteh.irc:client-lib:3.2.3'
}

license {
    ext.name = project.name
    ext.organization = project.organization
    ext.url = project.url
    header rootProject.file('HEADER.txt')
    ignoreFailures true
    strictCheck true
    include '**/*.java'
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

if (JavaVersion.current().isJava8Compatible() || JavaVersion.current().isJava9Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

compileJava {
    options.compilerArgs += [
            '-Xlint:-processing',
    ]
    options.deprecation = true
    options.encoding = 'utf8'
}

jar {
    classifier 'base'

    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': url
        )
    }
}

shadowJar {
    mainSpec.sourcePaths.clear()
    classifier ''
    dependencies {
        include dependency('ninja.leaping.configurate:configurate-hocon')
    }
}

artifacts {
    archives shadowJar
}
